plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

apply from: rootProject.file('dependencies.gradle')

sourceCompatibility = JavaVersion.VERSION_11

configurations {
    compileExternal
    implementation.extendsFrom compileExternal
}

dependencies {
    implementation project(':libcomp')
    implementation project(':libjds')

    implementation group: 'com.flash3388.flashlib', name: 'flashlib', version: '2.0.0'
    implementation group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.8.1'
    implementation gson()
    implementation castle()
    implementation slf4jApi()
    implementation controlfx()

    implementation 'com.github.tomtzook:jsdl2:0.1.0'
    compileExternal group: 'com.github.tomtzook', name: 'jsdl2-jni', version: '0.1.0', classifier: 'linux-amd64'

    testImplementation junit()
    testImplementation mockito()
    testImplementation hamcrest()
}

jar {
    from {
        configurations.compileExternal.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "failed"
        exceptionFormat "full"
        showStackTraces true
    }
}

javafx {
    version = '11'
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.base']
}

application {
    mainModule = 'driverstation.main'
    mainClassName = 'com.flash3388.frc.ds.Main'
    applicationName = 'JDriverStation'
}

startScripts {
    defaultJvmOpts = ['-Djdk.gtk.version=2']
}

run {
    main = "$moduleName/com.flash3388.frc.ds.Main"
    jvmArgs '-Djdk.gtk.version=2'
    args '--debug', '--nofiles'
}